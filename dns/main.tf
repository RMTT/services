terraform {
  required_providers {
    cloudflare = {
      source  = "cloudflare/cloudflare"
      version = "~> 5"
    }
    adguard = {
      source = "gmichels/adguard"
      version = "~> 1.7.0"
    }
    sops = {
      source  = "carlpett/sops"
      version = "~> 1.4.1"
    }
  }
}

data "sops_file" "secrets" {
  source_file = "keys.yaml"
}

data "sops_file" "hosts" {
  source_file = "hosts"
  input_type = "raw"
}

provider "cloudflare" {
  api_token = data.sops_file.secrets.data["CLOUDFLARE_API_TOKEN"]
}

provider "adguard" {
  host     = "homerouter.java-crocodile.ts.net:3000"
  username = data.sops_file.secrets.data["adguard_username"]
  password = data.sops_file.secrets.data["adguard_password"]
  scheme   = "http"
  insecure = false
}

variable "cf_record_id_file" {
  type    = string
  default = "./cf_ids.yaml"

  validation {
    condition     = fileexists(var.cf_record_id_file)
    error_message = "⚠️ cannot find ./cf_ids.yaml, please create it via 'sops exec-env ./keys.yaml cf-record-ids'"
  }
}

locals {
  zone_lookup = {
    "rmtt.tech" = data.sops_file.secrets.data["cf_zone_rmtt_tech"]
    "rmtt.fun"  = data.sops_file.secrets.data["cf_zone_rmtt_fun"]
    "rmtt.host" = data.sops_file.secrets.data["cf_zone_rmtt_host"]
  }

  cf_ids = yamldecode(file(var.cf_record_id_file))
  import_dns = {
    for domain, config in local.filtered_dns :
    domain => config
    if lookup(local.cf_ids, domain, null) != null
  }

  raw_list = nonsensitive(yamldecode(data.sops_file.hosts.raw))
  all_dns = {
    for x in local.raw_list : "${x.name}.${x.zone}" => x
  }
  filtered_dns = {
    for x in local.raw_list : "${x.name}.${x.zone}" => x
    if x.push == true
  }
}

resource "adguard_rewrite" "dynamic" {
  for_each = { for x in local.all_dns : "${x.name}.${x.zone}" => x }

  domain = each.key
  answer = each.value.value
}

import {
  for_each = local.import_dns

  to = cloudflare_dns_record.dynamic[each.key]
  id = "${local.zone_lookup[each.value.zone]}/${local.cf_ids[each.key]}"
}

resource "cloudflare_dns_record" "dynamic" {
  for_each = { for x in local.filtered_dns : "${x.name}.${x.zone}" => x }

  zone_id = local.zone_lookup[each.value.zone]
  name    = each.value.name
  content = each.value.value
  type    = "A"
  ttl     = 1
  comment = "records created by terraform"
}
